use std::str::FromStr;
use ast;

grammar;

// https://github.com/antlr/grammars-v4/blob/master/json/JSON.g4
// Taken from "The Definitive ANTLR 4 Reference" by Terence Parr
// Derived from http://json.org

pub json: ast::Value = {
    <v:value> => {
        match v {
            // embed top-level array in table
            ast::Value::Array(_) => {
                let mut map = ast::Table::new();
                map.push(("json".to_string(), v));
                ast::Value::Table(map)
            },
            _ => v
        }
    }
};

object: ast::Table = {
    "{" <p0:pair> <pairs:("," <pair>)*> "}" => {
        let mut map = ast::Table::new();
        map.push(p0);
        for pair in pairs {
            map.push(pair);
        }
        map
    },
    "{" "}" => ast::Table::new()
};

pair: (String, ast::Value) = {
    <k:STRING> ":" <v:value> => {
        // convert mixed "stops" array to table
        let mut is_stops = false;
        if k == "stops" {
            if let ast::Value::Array(ref arr) = v {
                is_stops = arr.iter().all(|el|
                    match el { &ast::Value::Array(ref stoparr) => stoparr.len() == 2, _ => false }
                );
            }
        }

        let mut kvpair = (String::from(k), v.clone());

        if is_stops {
            if let ast::Value::Array(ref stops) = v {
                let stoptables = stops.iter().map(|stoparr| {
                    let mut table = ast::Table::new();
                    if let &ast::Value::Array(ref stop) = stoparr {
                        table.push(("in".to_string(), stop[0].clone()));
                        table.push(("out".to_string(), stop[1].clone()));
                    }
                    ast::Value::Table(table)
                }).collect();
                kvpair.1 = ast::Value::Array(stoptables);
            }
        }

        kvpair
    }
};

array: ast::Array = {
    "[" <v0:value> <values:("," value)*> "]" => {
        let mut arr = Vec::with_capacity(values.len()+1);
        arr.push(v0);
        for (_, val) in values {
            arr.push(val);
        }

        // Fix element type of combining filters
        let mut is_filter = false;
        if arr.len() > 1 {
            if let ast::Value::String(ref s) = arr[0] {
                if vec!["all", "any", "none"].iter().any(|keyword| s == keyword) {
                    is_filter = arr[1..arr.len()-1].iter().all(|el|
                        match el { &ast::Value::Array(_) => true, _ => false }
                    );
                }
            }
        }
        if is_filter {
            arr[0] = ast::Value::Array(vec![arr[0].clone()]);
        }

        arr
    },
    "[" "]" => Vec::new()
};

pub value: ast::Value = {
    STRING => ast::Value::String(<>),
    NUMBER,
    object => ast::Value::Table(<>),
    array => ast::Value::Array(<>),
    "true" => ast::Value::Boolean(true),
    "false" => ast::Value::Boolean(false),
    "null" => ast::Value::String(String::from("")),
};


STRING: String = {
    r#""([^"\\]|\\["\\/bfnrt]|\\u[0-9a-fA-F][0-9a-fA-F][0-9a-fA-F][0-9a-fA-F])*""# => String::from(&<>[1 .. <>.len()-1])
};

NUMBER: ast::Value = {
    <m:"-"?> <i:INT> <n:r"\.[0-9]+"> <e:EXP?> => {
        let sign = m.unwrap_or("+");
        let exp = e.unwrap_or(String::new());
        let f = f64::from_str(&format!("{}{}{}{}", sign, i, n, &exp)).unwrap();
        ast::Value::Float(f)
    },
    <i:INT> <e:EXP> => ast::Value::Float(f64::from_str(&format!("{}{}", i, &e)).unwrap()),
    "-" <i:INT> <e:EXP> => ast::Value::Float(f64::from_str(&format!("-{}{}", i, &e)).unwrap()),
    INT => ast::Value::Integer(i64::from_str(<>).unwrap()),
    "-" <num: INT> => ast::Value::Integer(-i64::from_str(num).unwrap()),
};

INT: &'input str = {
    "0",
    r"[1-9][0-9]*",
};

// no leading zeros

EXP: String = {
    r"[Ee][+-]?" INT => { format!("{}{}", <>) }
};
